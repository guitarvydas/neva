drawio {
  Mxfile = "<" "mxfile" Attribute* ">" DiagramTab+ "</mxfile>"
  DiagramTab = 
    | "<" "diagram" Name ID ">" MxGraphModel "</diagram>" -- withContent
    | "<" "diagram" Attribute* "/>"                       -- noContent
  MxGraphModel = "<" "mxGraphModel" Attribute* ">" Root "</mxGraphModel>"
  Root = "<" "root" ">" Cell+ "</root>"
  Cell =
    | "<" "mxCell" ID Parent "/>" -- cell1
    | "<" "mxCell" ID "/>"        -- cell0
    | "<" "mxCell" Attribute* ">" CellContents "</mxCell>" -- other

    CellContents =
      | "<" "mxGeometry" Attribute* "/>" -- noContent
      | "<" "mxGeometry" Attribute* ">" GeometryContents "</mxGeometry>" -- withContent

    GeometryContents = GeometryItem+
    GeometryItem =
      | SourcePoint -- sourcePoint
      | TargetPoint -- targetPoint
      | Point -- point
      | RectangleGeometry -- rect
      | ArrayGeometry -- array

    SourcePoint = "<" "mxPoint" Xcoord Ycoord "as" "=" dq "sourcePoint" dq "/>"
    TargetPoint = "<" "mxPoint" Xcoord Ycoord "as" "=" dq "targetPoint" dq "/>"
    Point = "<" "mxPoint" Xcoord Ycoord "/>"
    RectangleGeometry = "<" "mxRectangle" Attribute* "/>"
    ArrayGeometry = "<" "Array" Attribute* ">" GeometryContents "</Array>"

    Xcoord = "x" "=" numericString
    Ycoord = "y" "=" numericString
    
    Name = "name" "=" str
    ID = "id" "=" str
    Parent = "parent" "=" str
    Attribute =
      | sym "=" numericString -- num
      | sym "=" fractionalString -- frac
      | sym "=" str -- other

      sym = letter symFollow*
      symFollow = alnum | "_"
      notGT = ~">" any
      numericString = dq digit+ dq
      fractionalString = dq fdigit+ dq
      fdigit = digit | "."
      str = dq char* dq
      dq = "\""
      char =
	| "\\\""  -- escapedDQ
	| ~dq any -- other
}
