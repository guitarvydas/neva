drawio1 {
  Mxfile = "<" "mxfile" stuff ">" DiagramTab+ "</mxfile>"
  DiagramTab = 
    | "<" "diagram" Name ID ">" MxGraphModel "</diagram>" -- withContent
    | "<" "diagram" stuff "/>"                            -- noContent
  MxGraphModel = "<" "mxGraphModel" Attribute* ">" Root "</mxGraphModel>"
  Root = "<" "root" ">" Cell+ "</root>"
  Cell =
    | "<" "mxCell" ID Parent "/>" -- cell1
    | "<" "mxCell" ID "/>"        -- cell0
    | "<" "mxCell" Attribute* ">" CellContents "</mxCell>" -- other

    CellContents =
      | "<" "mxGeometry" Attribute* "/>" -- noContent
      | "<" "mxGeometry" Attribute* ">" GeometryContents "</mxGeometry>" -- withContent

    GeometryContents =
      | MxPoint+ -- mxpoints
    MxPoint =
      | SourcePoint -- sourcePoint
      | TargetPoint -- targetPoint

    SourcePoint = "<" "mxPoint" Xcoord Ycoord "as" "=" dq "sourcePoint" dq "/>"
    TargetPoint = "<" "mxPoint" Xcoord Ycoord "as" "=" dq "targetPoint" dq "/>"

    Xcoord = "x" "=" numericString
    Ycoord = "y" "=" numericString
    
    Name = "name" "=" str
    ID = "id" "=" str
    Parent = "parent" "=" str
    Attribute =
      | ID          -- id
      | Parent      -- parent
      | sym "=" str -- other

      sym = letter symFollow*
      symFollow = alnum | "_"
      stuff = notGT+
      notGT = ~">" any
      numericString = dq digit+ dq
      str = dq char* dq
      dq = "\""
      char =
	| "\\\""  -- escapedDQ
	| ~dq any -- other
}
